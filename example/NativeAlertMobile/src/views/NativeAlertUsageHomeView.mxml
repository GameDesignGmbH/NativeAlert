<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="Native Alerts" creationComplete="view_creationComplete()" backKeyPressed="view1_backKeyPressedHandler(event)"
		>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.utils.ObjectUtil;
			
			import pl.mateuszmackowiak.nativeANE.LogEvent;
			import pl.mateuszmackowiak.nativeANE.NativeDialogEvent;
			import pl.mateuszmackowiak.nativeANE.NativeDialogListEvent;
			import pl.mateuszmackowiak.nativeANE.NativeExtensionErrorEvent;
			import pl.mateuszmackowiak.nativeANE.alert.NativeAlert;
			import pl.mateuszmackowiak.nativeANE.alert.NativeAlertEvent;
			import pl.mateuszmackowiak.nativeANE.dialogs.NativeListDialog;
			import pl.mateuszmackowiak.nativeANE.dialogs.NativeTextInput;
			import pl.mateuszmackowiak.nativeANE.dialogs.NativeTextInputDialog;
			import pl.mateuszmackowiak.nativeANE.dialogs.NativeTextInputDialogEvent;
			import pl.mateuszmackowiak.nativeANE.progress.NativeProgress;
			import pl.mateuszmackowiak.nativeANE.properties.SystemProperties;
			import pl.mateuszmackowiak.nativeANE.toast.Toast;
			
			import spark.events.IndexChangeEvent;
			import spark.events.ViewNavigatorEvent;
			
			
			
			
			private var progressPopup:NativeProgress;
			private var p:int = 0;
			private var myTimer:Timer = new Timer(100);
			private var multChDialog:NativeListDialog=null;
			private var singleChDialog:NativeListDialog=null;
			private var textInputDialog:NativeTextInputDialog=null;
			private var choces:Vector.<String>=null;
			private var checkedItems:Vector.<Boolean>=null;
			private var selectedIndex:int = -1;
		
			protected function view_creationComplete():void
			{
				FlexGlobals.topLevelApplication.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR,onError);
				FlexGlobals.topLevelApplication.addEventListener(NativeExtensionErrorEvent.ERROR,onError);
				
				NativeApplication.nativeApplication.addEventListener(Event.EXITING,exiting);
								
				if(NativeAlert.isSupported){
					returnText.text+= "extension IS supported on this device\n";
				}else{
					returnText.text+= "extension is NOT supported on this device\n";
				}
				choces = new Vector.<String>();
				choces.push("one","two","three","four");
				checkedItems = new Vector.<Boolean>();
				checkedItems.push(true,false,false,true);
				
				if(Capabilities.os.indexOf("Linux")>-1){
					titleInput.text = "Some <b>simple HTML</b> Title";
					messageInput.text = "Some <i>simple HTML</i> Message Text";
				}else{
					titleInput.text = "Some Title";
					messageInput.text = "Some Message Text";
				}
				if(SystemProperties.isSupported()){
					var dictionary:Dictionary = SystemProperties.getAndroidProperites(); 
					
					returnText.text += SystemProperties.ARCHITECTURE+"  "+dictionary[SystemProperties.ARCHITECTURE]+"\n";
					returnText.text += SystemProperties.JAVA_NUMBER+"  "+dictionary[SystemProperties.JAVA_NUMBER]+"\n";
					returnText.text += SystemProperties.LANGUAGE+"  "+dictionary[SystemProperties.LANGUAGE]+"\n";
					returnText.text += SystemProperties.OS+"  "+dictionary[SystemProperties.OS]+"\n";
					returnText.text += SystemProperties.VERSION+"  "+dictionary[SystemProperties.VERSION]+"\n";
					
					returnText.text += SystemProperties.PACKAGE_DIRECTORY+"  "+dictionary[SystemProperties.PACKAGE_DIRECTORY]+"\n";
					returnText.text += SystemProperties.PACKAGE_NAME+"  "+dictionary[SystemProperties.PACKAGE_NAME]+"\n";
					returnText.text += SystemProperties.APP_UID+"  "+dictionary[SystemProperties.APP_UID]+"\n";
					returnText.text += SystemProperties.UID+"  "+dictionary[SystemProperties.UID]+"\n";
					
					dictionary = null;
				}
				
			}
			protected function exiting(event:Event):void
			{
				if(progressPopup)
					progressPopup.kill();
				if(multChDialog)
					multChDialog.kill();
				if(textInputDialog)
					textInputDialog.kill();
				
				NativeAlert.dispose();
				Toast.dispose();
			}
			private function onError(event:*):void
			{
				if(event is UncaughtErrorEvent){
					const e:UncaughtErrorEvent = event;
					if(e.error is Error)
						returnText.text += "UncaughtErrorEvent Error "+ (e.error as Error).message+"   "+(e.error as Error).toString()+"\n";
					else
						returnText.text += "UncaughtErrorEvent ErrorEvent "+ (e.error as ErrorEvent).text+"   "+(e.error as ErrorEvent).toString()+"\n";
				}else
					returnText.text += "Error "+ event.text+"   "+event.toString()+"\n";
			}
			private function traceEvent(event:*):void
			{
				if(event.hasOwnProperty("text"))
					returnText.text += event.toString()+"  "+event.text+"\n";
				else if(event.hasOwnProperty("index"))
					returnText.text += event.toString()+"  "+event.index+"\n";
				else
					returnText.text += event.toString()+"\n";
			}
			protected function view1_backKeyPressedHandler(event:FlexEvent):void
			{
				event.preventDefault();
				returnText.text +="back key pressed\n";
				
				myTimer.removeEventListener(TimerEvent.TIMER,updateProgress);
				myTimer.stop();
				p = 0;
				if(progressPopup){
					progressPopup.hide();
				}
			}
			
			
			
			
			
			
			
			
			
			protected function callNativeAlret(event:MouseEvent):void
			{
				if(Capabilities.os.indexOf('Linux')>-1)
					NativeAlert.show(messageInput.text,titleInput.text,closeLabelInput.text, otherLabels.text , closeFun,canclebleCheckbox.selected,ThemeSelector.selectedItem.data);
				else
					NativeAlert.show(messageInput.text,titleInput.text,closeLabelInput.text, otherLabels.text , closeFun);
			}
			private function closeFun(event:NativeAlertEvent):void
			{
				var n:NativeAlert = event.target as NativeAlert;
				returnText.text+= "the answer: "+event.index+"\n";
			}
			
			
			
			
			
			
			
			
			protected function openProgressPopup(style:int):void
			{
				try{
					progressPopup = new NativeProgress(style);
					progressPopup.setSecondaryProgress(45);
					progressPopup.addEventListener(NativeDialogEvent.CANCLED,closeNativeProcessHandler);
					progressPopup.addEventListener(NativeDialogEvent.OPENED,traceEvent);
					progressPopup.addEventListener(NativeDialogEvent.CLOSED,closeNativeProcessHandler);
					progressPopup.addEventListener(NativeExtensionErrorEvent.ERROR,onError);
					progressPopup.addEventListener(LogEvent.LOG_EVENT,traceEvent);
					progressPopup.theme = ThemeSelector.selectedItem.data;
					progressPopup.setMax(50);
					
					progressPopup.show(0, titleInput.text , messageInput.text,canclebleCheckbox.selected,indeterminateInput.selected);
					myTimer.addEventListener(TimerEvent.TIMER, updateProgress);
					myTimer.start();
				}catch(e:Error){
					returnText.text += "Error "+ e.message+"   "+e.toString()+"\n";
				}
			}
			private function closeNativeProcessHandler(event:*):void
			{
				returnText.text += event.toString()+"\n";
				progressPopup.removeEventListener(NativeDialogEvent.CANCLED,closeNativeProcessHandler);
				progressPopup.removeEventListener(NativeDialogEvent.CLOSED,closeNativeProcessHandler);
				progressPopup.removeEventListener(NativeDialogEvent.OPENED,traceEvent);
				progressPopup.removeEventListener(NativeExtensionErrorEvent.ERROR,onError);
				progressPopup.removeEventListener(LogEvent.LOG_EVENT,traceEvent);
				progressPopup.kill();
				
				myTimer.removeEventListener(TimerEvent.TIMER,updateProgress);
				myTimer.stop();
				p = 0;
			}
			private function updateProgress(event:TimerEvent):void
			{
				p++;
				if(p>=50){
					p = 0;
					progressPopup.hide();
					(event.target as Timer).stop();
				}else{
					try{
						if(progressPopup.getIndeterminate()==false)
							progressPopup.setProgress(p);
					}catch(e:Error){
						returnText.text += "Error "+ e.message+"   "+e.toString()+"\n";
					}
				}
				
			}
			
			
			
			
			public function showToast(event:MouseEvent):void
			{
				Toast.show(messageInput.text,Toast.LENGTH_LONG);
			}
			
			public function showCenteredToast(event:MouseEvent):void
			{
				var randX:int = Math.random()*600;
				var randY:int = Math.random()*600;
				Toast.showWithDifferentGravit(messageInput.text,Toast.LENGTH_SHORT,Toast.GRAVITY_LEFT,randX,randY);
			}

			
			private function onTextInputDialogClosedHandler(event:NativeTextInputDialogEvent):void
			{
				for each (var n:NativeTextInput in event.list) 
				{
					returnText.text +=n.name+":  "+n.text+"\n";
				}
				returnText.text +="button selected "+event.buttonIndex.toString()+"\n";
				
			}
			
			public function openTextInputDialog(event:MouseEvent):void{
				textInputDialog = new NativeTextInputDialog();
				textInputDialog.theme = ThemeSelector.selectedItem.data;
				textInputDialog.addEventListener(NativeDialogEvent.CANCLED,closeTextInputDialogHandler);
				textInputDialog.addEventListener(NativeDialogEvent.OPENED,traceEvent);
				textInputDialog.addEventListener(NativeExtensionErrorEvent.ERROR,onError);
				textInputDialog.addEventListener(LogEvent.LOG_EVENT,traceEvent);
				textInputDialog.addEventListener(NativeTextInputDialogEvent.CLOSED,onTextInputDialogClosedHandler);
				var a:Array = otherLabels.text.split(",");
				var buttons:Vector.<String> = new Vector.<String>();
				if(a==null || a.length==0)
					buttons.push(closeLabelInput.text);
				else{
					a.unshift(closeLabelInput.text);
					for each (var button:String in a) 
					{
						buttons.push(button);
					}
				}

				var v:Vector.<NativeTextInput> = new Vector.<NativeTextInput>();
				var ti1:NativeTextInput = new NativeTextInput("name");
				ti1.inputType = NativeTextInput.text;
				ti1.messageBefore ="name:";
				ti1.text = "jacek";
				v.push(ti1);
				var ti2:NativeTextInput = new NativeTextInput("password");
				ti2.inputType = NativeTextInput.textPassword;
				ti2.messageBefore ="password:";
				ti2.text = "dddd";
				v.push(ti2);
				textInputDialog.show(titleInput.text,v,buttons);
			}
			public function closeTextInputDialogHandler(event:NativeDialogEvent):void
			{
				if(event.hasOwnProperty("index"))
					returnText.text += event.toString()+"   "+event.index+"\n";
				else
					returnText.text += event.toString()+"\n";
				textInputDialog.removeEventListener(NativeDialogEvent.CANCLED,closeTextInputDialogHandler);
				textInputDialog.removeEventListener(NativeDialogEvent.CLOSED,closeTextInputDialogHandler);
				textInputDialog.removeEventListener(NativeDialogEvent.OPENED,traceEvent);
				textInputDialog.removeEventListener(NativeExtensionErrorEvent.ERROR,onError);
				textInputDialog.removeEventListener(LogEvent.LOG_EVENT,traceEvent);
				textInputDialog.kill();
			}
			
			
			
			
			public function openSingleChoiceDialog(event:MouseEvent):void
			{
				singleChDialog = new NativeListDialog();
				singleChDialog.theme = ThemeSelector.selectedItem.data;
				
				singleChDialog.addEventListener(NativeDialogEvent.CANCLED,closeNativeDialogHandler);
				singleChDialog.addEventListener(NativeDialogEvent.OPENED,traceEvent);
				singleChDialog.addEventListener(NativeDialogEvent.CLOSED,closeNativeDialogHandler);
				singleChDialog.addEventListener(LogEvent.LOG_EVENT,traceEvent);
				singleChDialog.addEventListener(NativeExtensionErrorEvent.ERROR,onError);
				
				singleChDialog.addEventListener(NativeDialogListEvent.LIST_CHANGE,onListChange);
				var a:Array = otherLabels.text.split(",");
				var buttons:Vector.<String> = new Vector.<String>();
				if(a==null || a.length==0)
					buttons.push(closeLabelInput.text);
				else{
					a.unshift(closeLabelInput.text);
					for each (var button:String in a) 
					{
						buttons.push(button);
					}
				}
				singleChDialog.showSingleChoice(choces,selectedIndex,buttons,titleInput.text,canclebleCheckbox.selected);
			}
			
			
			protected function openMultiChoiceDialog(event:MouseEvent):void
			{
				multChDialog = new NativeListDialog();
				multChDialog.theme = ThemeSelector.selectedItem.data;
				
				multChDialog.addEventListener(NativeDialogEvent.CANCLED,closeNativeDialogHandler);
				multChDialog.addEventListener(NativeDialogEvent.OPENED,traceEvent);
				multChDialog.addEventListener(NativeDialogEvent.CLOSED,closeNativeDialogHandler);
				multChDialog.addEventListener(NativeExtensionErrorEvent.ERROR,onError);
				multChDialog.addEventListener(NativeDialogListEvent.LIST_CHANGE,onListChange);
				multChDialog.addEventListener(LogEvent.LOG_EVENT,traceEvent);
				var a:Array = otherLabels.text.split(",");
				var buttons:Vector.<String> = new Vector.<String>();
				if(a==null || a.length==0)
					buttons.push(closeLabelInput.text);
				else{
					a.unshift(closeLabelInput.text);
					for each (var button:String in a) 
					{
						buttons.push(button);
					}
				}
				multChDialog.showMultiChoice(choces,checkedItems,buttons,titleInput.text,canclebleCheckbox.selected);
			}
			
			private function onListChange(event:NativeDialogListEvent):void
			{
				var dialog:NativeListDialog = (event.target as NativeListDialog)
				returnText.text += "NativeDialogListEvent "+ event.index+"   "+event.selected+"\n";
				if(dialog.selectedIndex>-1){
					selectedIndex = dialog.selectedIndex;
					returnText.text += "selectedIndex   "+selectedIndex+"\n";
				}else{
					const a:Vector.<String> = dialog.selectedLabels;
					returnText.text +="selectedIndexes : \n";
					if(a.length>0){
						for (var i:int = 0; i < a.length; i++) 
						{
							returnText.text +="   "+a[i]+"\n";
						}
					}
				}
					 
			}
			public function closeNativeDialogHandler(event:NativeDialogEvent):void
			{
				returnText.text += event.toString()+"\n";
				var dialog:NativeListDialog = (event.target  as NativeListDialog);
				dialog.removeEventListener(NativeDialogEvent.CANCLED,closeNativeDialogHandler);
				dialog.removeEventListener(NativeDialogEvent.CLOSED,closeNativeDialogHandler);
				dialog.removeEventListener(NativeDialogEvent.OPENED,traceEvent);
				dialog.removeEventListener(NativeExtensionErrorEvent.ERROR,onError);
				dialog.removeEventListener(LogEvent.LOG_EVENT,traceEvent);
				dialog.removeEventListener(LogEvent.LOG_EVENT,traceEvent);
				dialog.kill();
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:ArrayList id="themes">
			<fx:Object name="THEME_DEVICE_DEFAULT_DARK" data="0x00000004"/>
			<fx:Object name="THEME_DEVICE_DEFAULT_LIGHT" data="0x00000005"/>
			<fx:Object name="THEME_HOLO_DARK" data="0x00000002"/>
			<fx:Object name="THEME_HOLO_LIGHT" data="0x00000003"/>
			<fx:Object name="THEME_TRADITIONAL" data="0x00000001"/>
		</s:ArrayList>
	</fx:Declarations>
	<s:layout>
		<s:VerticalLayout horizontalAlign="center"/>
	</s:layout>
		
	<s:HGroup height="100%" width="100%">
		<s:Spacer width="100%"/>
		<s:VGroup>
			<s:TextInput id="titleInput" />
			<s:TextInput id="messageInput" />
			<s:TextInput id="closeLabelInput" text="OK" />
			<s:TextInput id="otherLabels" text="YES,NO" />
			<s:CheckBox id="canclebleCheckbox" label="cancleble" selected="true"/>
		</s:VGroup>
		<s:SpinnerListContainer includeInLayout="{Capabilities.os.indexOf('Linux')>-1}">
			<s:SpinnerList id="ThemeSelector" fontWeight="bold" labelField="name" minWidth="300" dataProvider="{themes}" selectedItem="{themes.getItemAt(0)}"/>
		</s:SpinnerListContainer>
		<s:Spacer width="100%"/>
	</s:HGroup>
	
	
	
	<s:Button width="100%" click="callNativeAlret(event)" label="call native Alert" height="60"/>
	
	<s:VGroup width="100%" height="100%" >
		<s:HGroup width="100%" includeInLayout="{NativeProgress.isSupported}">
			<s:Button width="100%" label="show horizontal progress popup" click="openProgressPopup(NativeProgress.STYLE_HORIZONTAL)" height="60"/>
			<s:CheckBox id="indeterminateInput" label="indeterminate" selected="true"/>
			<s:Button width="100%" label="show spinner progress popup" click="openProgressPopup(NativeProgress.STYLE_SPINNER)" height="60"/>
		</s:HGroup>
		<s:HGroup width="100%" includeInLayout="{NativeListDialog.isSupported}">
			<s:Button width="100%" click="openMultiChoiceDialog(event)" label="open Multi Choice Dialog" height="60" />
			<s:Button width="100%" click="openSingleChoiceDialog(event)" label="open Single Choice Dialog" height="60" />
		</s:HGroup>
		<s:HGroup width="100%"  includeInLayout="{Toast.isSupported}" >
			<s:Button width="100%" click="showToast(event)" label="show Toast" height="60"/>
			<s:Button width="100%" click="showCenteredToast(event)" label="show Toast in random gravity" height="60"/>
		</s:HGroup>
	
		<s:Button width="100%" click="openTextInputDialog(event)" label="open Text Input Dialog" height="60" includeInLayout="{NativeTextInputDialog.isSupported}" />
	</s:VGroup>
		
	<s:TextArea id="returnText" width="100%" height="100%" selectable="false" editable="false" minHeight="500"/>
</s:View>
